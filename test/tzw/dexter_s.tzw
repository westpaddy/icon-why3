scope Unknown

  predicate pre (c : ctx) =
    let token_pool, (xtz_pool, token_contract) = c.cpmm_store in
    let token_ledger, approval = c.fa_store in
    token_contract = fa /\
    approval[cpmm] = const false /\
    token_pool.nat_v <= token_ledger[cpmm].nat_v

  predicate post (c : ctx) (c' : ctx) =
    let token_pool', (xtz_pool', token_contract') = c'.cpmm_store in
    let token_ledger', approval' = c'.fa_store in
    token_contract' = fa /\
    approval'[cpmm] = const false /\
    token_pool'.nat_v <= token_ledger'[cpmm].nat_v

end

scope Cpmm

  type param = (nat, (address, address))

  type store = (nat, (nat, address))

  predicate spec (st : step) (p : (nat, (address, address))) (s : (nat, (nat, address))) (ops : list operation) (s' : (nat, (nat, address))) =
    let token_pool, (xtz_pool, token_contract) = s in
    let token_pool', (xtz_pool', token_contract') = s' in
    let token_sold, (token_owner, receipient) = p in
    let b = div (token_sold.nat_v * xtz_pool.nat_v) (token_sold.nat_v + token_pool.nat_v) in
    token_sold.nat_v > 0 /\
    (*token_owner <> st.self /\*)
    token_pool'.nat_v = token_pool.nat_v + token_sold.nat_v /\
    xtz_pool'.nat_v = xtz_pool.nat_v - b /\
    token_contract = token_contract' /\
    ops = Cons (Xfer (fa_gparam (Left (token_owner, (st.self, token_sold)))) (mk_mutez 0) token_contract)
                (Cons (Xfer (PUnit ()) (mk_mutez b) receipient)
                  Nil)

  let upper_ops = 2

  predicate pre (c : ctx) = inv_pre c

  predicate post (st : step) (p : (nat, (address, address))) (c : ctx) (c' : ctx) = inv_post c c'

end

scope Fa

  type param = (or (address, (address, nat)) address)

  type store = (map address nat, map address (map address bool))

  predicate spec (st : step) (p : (or (address, (address, nat)) address)) (s : (map address nat, map address (map address bool))) (ops : list operation) (s' : (map address nat, map address (map address bool))) =
    let (ledger, approval) = s in
    let (ledger', approval') = s' in
    match p with
    | Left (_from, (_to, _val)) ->
      approval = approval' /\
      (let l = ledger[_from <- mk_nat (ledger[_from].nat_v - _val.nat_v)] in
         ledger' = l[_to <- mk_nat (l[_to].nat_v + _val.nat_v)]) /\
      (_from = st.sender || approval[_from][st.sender]) /\ (_val.nat_v <= ledger[_from].nat_v) /\
      ops = Nil
    | Right _spender ->
      ledger = ledger' /\
      approval[st.sender <- approval[st.sender][_spender <- true]] = approval' /\
      ops = Nil
    end

  let upper_ops = 0

  predicate pre (c : ctx) = true

  predicate post (st : step) (p : (or (address, (address, nat)) address)) (c : ctx) (c' : ctx) =
    c.cpmm_store = c'.cpmm_store /\
    fa_spec st p c.fa_store Nil c'.fa_store

end
